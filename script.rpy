# coding: utf-8
image bg lecture_hall = "images/lecture_hall.png"  
image black_screen = Solid("#000000")
image library = "images/library.png"
image living_room = "images/living_room.png"
image library_it = "images/library_it.png"
image colourful_library = "images/colourful_library.png"
image cafe = "images/cafe.png"
image bedroom = "images/bedroom.png"
image document = "images/document.png"
image it_company = "images/it_company.png"


image teacher = im.Scale("images/teacher.png", int(800 * (505 / 1142)), 800)
image neighbor = im.Scale("images/neighbor.png", int(800 * (505 / 1142)), 800)
image maks = im.Scale("images/maks.png", int(800 * (505 / 1142)), 800)
image maks_end = im.Scale("images/maks_end.png", int(800 * (505 / 1142)), 800)
image lera = im.Scale("images/lera.png", int(800 * (505 / 1142)), 800)
image father = im.Scale("images/father.png", int(800 * (505 / 1142)), 800)
image mother = im.Scale("images/mother.png", int(800 * (505 / 1142)), 800)
image anna = im.Scale("images/anna.png", int(800 * (505 / 1142)), 800)
image andreiy =im.Scale("images/andreiy.png", int(800 * (505 / 1142)), 800)
image sokursnik =im.Scale("images/sokursnik.png", int(800 * (505 / 1142)), 800)



define teacher = Character("Преподаватель")
define neighbor = Character("Сосед по парте", who_color="#e67e22")  
define maks = Character("Макс", who_color="#3498db")
define lera = Character("Лера", who_color="#119727") 
define father = Character("Отец", who_color="#D73F34") 
define mother = Character("Мать", who_color="#D73F34") 
define anna = Character("Анна", who_color="#E7F33D") 
define andreiy = Character("Андрей", who_color="#119727")
define sokursnik = Character("Сокурсник", who_color="#119727")

default total_points = 0

# 505 1142

label start:
    call scene_one from _call_scene_one  
    call scene_two from _call_scene_two  
    call scene_three from _call_scene_three 
    call scene_five from _call_scene_five
    call scene_four from _call_scene_four
    call scene_six from _call_scene_six

    return

label scene_one:
    scene black_screen
    "Серое утро в университете, в сером и унылом лекционном зале."
    teacher "Таким образом, мы видим, что толкование нормы права требует от нас точного подхода…"
    "Я уже не могу. Каждая лекция превращается в пытку."
    "Как же я сюда попал?"

    scene bg lecture_hall with dissolve:
        zoom 1.41
    show teacher at Position(xpos = 0.8, ypos = 1.1)
    show maks at Position(xpos = 0.2, ypos = 1.1)

    teacher "Макс, вы что-то хотите сказать? Или уже начнете писать конспект?"
    play sound "sms.mp3"

    menu:
        "Нет, ничего, извините…":
            maks "Нет, ничего, извините…"
        "Я пишу, пишу…":
            maks "Я пишу, пишу…"
        "*промолчать*":
            maks "*промолчать*"
    
    
    teacher "Убедитесь, что вам ясно, ведь пропуск ключевых аспектов приведет к слабым результатам на экзаменах."

    scene black_screen with dissolve
    "Экзамены… да кому они вообще нужны. Моё будущее точно не здесь."
    scene bg lecture_hall with dissolve:
        zoom 1.41

    hide teacher
    show maks at Position(xpos = 0.2, ypos = 1.1)
    show neighbor at Position(xpos = 0.8, ypos = 1.1)

    neighbor "Снова ворчишь? Ты же на юриста хотел."
    menu:
        "Нет, меня сюда отправили. Мне никогда это не нравилось,
        просто… Родители сказали, что «это — престижно».":
            maks "Нет, меня сюда отправили. Мне никогда это не нравилось,
        просто… Родители сказали, что «это — престижно»."
            neighbor " Ну, не тебе одному. У меня тоже выбора не было — по крайней
мере, ты неплох в этом. Я даже учебники с трудом читаю."

        "Хотел… Пф, да если бы.":
            maks "Хотел… Пф, да если бы."
            neighbor "У меня тоже выбора не было — по крайней мере, ты неплох в
этом. Я даже учебники с трудом читаю."

        "Отстань, и без тебя тошно.":
            maks "Отстань, и без тебя тошно."
            neighbor "Можно было бы и повежливее… "
            return

    maks "Дело не в этом. Я с детства компьютерами увлекался, программировал немного."
    maks "Но они даже не выслушали, когда я сказал, что хочу связать с этим свою жизнь."

    neighbor "Понимаю… но почему бы тебе не уйти, раз это так важно для тебя?"
    menu:
        "Просто уйти? (ухмыляется) Как будто родители не заставят
        вернуться… или не перестанут общаться со мной вообще.":
            maks "Просто уйти? (ухмыляется) Как будто родители не заставят
        вернуться… или не перестанут общаться со мной вообще."

        "Знал бы ты моих родителей, так бы не говорил…":
            maks "Знал бы ты моих родителей, так бы не говорил…"

        "Важно, не важно, какая им разница на это. Для них лишь
        важна «престижность» профессии.":
            maks "Важно, не важно, какая им разница на это. Для них лишь
        важна «престижность» профессии."

    neighbor "Ну, может, всё ещё получится. Не все такие жесткие, какими они кажутся."

    scene black_screen with dissolve:
        zoom 1.41
    pause 1.5

    return

label scene_two:
        
        "Писать конспекты, учить определения, да кому это
    нужно…"
        "Я всей душой ненавижу эту профессию и выбор своих
    родителей. Вот возьму и отчислюсь и поступлю куда всегда
    хотел. "
        "А может не стоит… Я всё-таки уже на третьем курсе, ещё
    немного и у меня будет диплом.
    "
        "Но так ли он мне нужен?.. "
        " Тяжело... Вот бы кто подсказал, дал направление, помог
    определиться с профессией. "
        "Мне этого так сейчас не хватает. "

        return


default job_points = 0

label scene_three:
    #scene black_screen
    scene library:
        zoom 1.41


    show maks at Position(xpos = 0.2, ypos = 1.1)
    show lera at Position(xpos = 0.8, ypos = 1.1)

    "Ой, а кто это? Выглядит не так, как все. Как-то…
Ярче что-ли? Подойду познакомиться."

    maks "Привет, ты тоже из университета? Не припомню тебя раньше."
    lera "Привет! Да, не из этого вуза. Я просто готовлюсь к сдаче
одного сложного теста. Я учусь на направлении
кибербезопасности."
    maks "(с интересом) Кибербезопасность? А что именно ты
делаешь?"
    lera "(с улыбкой) Пробую работать в роли пентестера. Ну, это как
бы белый хакер — тестирую системы на прочность, проверяю,
смогу ли обойти защиту."
    maks "Белый хакер? То есть ты легально взламываешь системы?
Как это вообще работает?"
    lera "Примерно так. Представь себе здание с кучей дверей и окон.
Моя задача — найти слабые места, через которые кто-то может
пробраться, и сообщить об этом владельцам, чтобы они
укрепили защиту."
    maks "Это звучит как-то… круто. Вроде детективной работы."
    lera "(улыбается) Именно так я и чувствую себя, когда нахожу
уязвимости. Иногда это целый квест, отыскать слабое место.
Хочешь попробовать?"
    menu:
        "Да, конечно, всегда хотел попробовать что-то такое!":
            maks "Да, конечно, всегда хотел попробовать что-то такое!"
            jump job

        "Но... я ведь учусь на юриста, так что у меня скорее всего не
        выйдет.":
            maks "Но... я ведь учусь на юриста, так что у меня скорее всего не выйдет."
            lera "Это профессия для людей с разным бэкграундом. Нужен аналитический склад ума и навык поиска решений. Многие приходят в кибербезопасность после работы в других сферах, так что и у тебя получится, смелее!"
            jump job


        "Нет, спасибо, я не настолько крут, чтобы вот так сразу
решить что-то.":
            maks "Нет, спасибо, я не настолько крут, чтобы вот так сразу
решить что-то."
            lera "Это профессия для людей с разным бэкграундом. Нужен аналитический склад ума и навык поиска решений. Многие приходят в кибербезопасность после работы в других
сферах, так что и у тебя получится, смелее!"
            maks "Нет-нет, не стоит… "
            lera "Ну, как знаешь. "
            jump no_job


label job:
    scene black_screen with dissolve
    play sound "keyboard.wav"
    pause 3
    stop sound fadeout 2.0
    pause 2
    scene library with dissolve:
        zoom 1.41
    
    show maks at Position(xpos = 0.2, ypos = 1.1)
    show lera at Position(xpos = 0.8, ypos = 1.1)

    $ job_points = 1
    $ total_points += 1

    maks "Вау, мне так понравилось работать в этой сфере!"
    lera "Видишь, Я же говорила, что всё не так сложно, как ты думаешь."
    maks "И тебя этому учат в университете?"
    lera "Да! На самом деле, учёба может приносить столько удовольствия, если тебе нравится твоя профессия."
    return

label no_job:
    $ job_points = 0
    $ total_points -= 1

    maks "А какие у тебя цели? Как ты видишь свою карьеру?"
    lera "О, хочу работать в крупной компании, как цифровой охранник. Если честно, в какой-то степени я оберегаю чьи-то данные и спасаю людей от потерь."
    maks "Звучит интересно, спасибо, что показала как у тебя все устроено. Может быть даже подумаю об отчислении."
    lera "Ого, это достаточно смелый шаг, надеюсь у тебя все получится. Удачи!"
    maks "Спасибо. И тебе удачи, пока. "
    lera "Пока!"

    return



label scene_four:
    scene black_screen
    scene living_room with dissolve:
        zoom 1.41
        
    show maks at Position(xpos = 0.2, ypos = 1.1)
    show father at Position(xpos = 0.7, ypos = 1.1)
    show mother at Position(xpos = 0.9, ypos = 1.1)

    maks "Мам, пап… Я тут хотел обсудить кое-что важное."
    father "Что-то случилось? У тебя проблемы с учёбой? Или нужны
деньги?"
    maks "Дело не в этом… я... Я давно уже думаю об этом, и,
кажется, пришло время вам сказать."
    mother "Говори уже, не томи нас."
    maks "Я хочу уйти с юрфака и поступить в айти-университет. Хочу
учиться на пентестера — проверять системы безопасности. Это
мой выбор."
    father "Макс, ты… это какая-то шутка? Взламывать компьютеры?
Ты серьёзно?"
    "В голосе отца сразу почувствовалась агрессия
и раздражённость, но я не намерен был отступать."
    maks "Это не просто взлом. Пентестеры работают легально, чтобы
защитить данные. Недавно я смог опробовать себя на простой
задаче. Я чувствую, что это то, чем я должен заниматься"
    mother "Но ты ведь уже три года отучился на юрфаке! А что ты
будешь делать после такого «переключения»? Устроишься какимто непонятным «хакером»?"
    maks "Вы никогда не спрашивали, чего хочу я. Я не хочу быть
юристом — это не моё. Я только и делаю, что страдаю на
каждой лекции."
    father "Макс, ты преувеличиваешь. Юридическое образование — это
стабильность, будущее. Айти — временное увлечение, где быстро
выгоришь."
    maks "Нет, это больше чем увлечение. Я понимаю, что мне нужно
учиться и работать, но это того стоит. Я чувствую, что смогу
добиться успеха."
    mother "И как ты представляешь свою жизнь после этого? Ты ведь
даже не знаешь, с чего начать…"
    maks "Знаю! Я общался с Лерой, она учится в айти-университете и
работает стажёром. Она уже занимается тестированием систем
на уязвимости."
    father "а стабильность и престиж тебе тоже Лера устроит?"
    maks "Это будущее, пап. Компании нуждаются в защите, ведь
атаки на данные случаются каждый день. Я хочу помогать
предотвращать это, а не прозябать на судебных делах."
    mother "Мы вложили столько усилий в твоё образование, а ты
хочешь всё бросить ради… какой-то компьютерной игры?"
    maks "Это не игра! Я помогу людям защитить их данные. Вы хоть
раз видели, как я заинтересованно занимался правом? Почему вы
меня не слышите?!"
    father "Потому что ты ведешь себя как ребёнок. Ты не видишь
реальности. Только айти и «хакеры» у тебя в голове!"
    maks "Да, потому что это то, чем я хочу заниматься! Я хочу быть
собой, а не жить чужой жизнью. Неужели так сложно понять?"
    mother "Макс, ты ещё молод. Ты просто не знаешь, что такое
настоящая работа и как важно иметь надёжность."
    maks "Я знаю, что хочу рисковать ради того, чтобы заниматься
любимым делом. Это куда лучше, чем медленно угасать в
нелюбимой профессии."
    father "Делай, что твоей душе угодно, но только не рассчитывай на
нашу помощь и поддержку."
    maks "Я вас понял. Просто… надеюсь, что когда-нибудь вы
сможете понять мой выбор."


    scene black_screen with dissolve:
        zoom 1.41
    pause 1.5

    return



label scene_five:
    scene black_screen
    scene cafe with dissolve:
        zoom 1.41

    show maks at Position(xpos = 0.2, ypos = 1.1)
    show anna at Position(xpos = 0.7, ypos = 1.1)
    show andreiy at Position(xpos = 0.9, ypos = 1.1)

    maks "Сказал я вчера родителям, что хочу уйти с юрфака."
    anna "Ого. И что они сказали?"
    maks "Взбесились. Говорят, что я просто капризничаю. Что
выбрал какие-то «взломы» вместо серьёзного будущего."
    andreiy "Да ладно, Макс, ты же давно хотел уйти. Они должны
понять."
    maks "Да не поймут они. У них своя картинка: юрист, семья,
стабильность. "
    anna "Слушай, ну это твоя жизнь. Ты сам то хоть уверен в своем
выборе?"

    if job_points == 1:
        maks "Уверен. Я встретил Леру, она учится на кибербезопасности. Пентестером хочет стать. Я когда услышал её историю, понял — это моё."
    else:
        maks "Не знаю. Я встретил Леру, она учится на кибербезопасности. Мне вроде как нравится это, но я будто мало что узнал о профессии"

    andreiy "Кибербезопасность? А что это вообще такое? Взламывать что-то?"

    if job_points == 1:
        maks "Ну, не совсем. Это тестирование безопасности. Например, у компании есть серверы, данные, базы. Пентестеры проверяют, можно ли их защиту обойти, как сделать их системы безопаснее."
        anna "Значит, ты как защитник данных?"
        maks "Именно. Если я буду этим заниматься, то смогу предотвратить кучу проблем для людей и компаний."
    else:
        maks "Ну что-то типа того."

    andreiy "Звучит круто. Но неужели это прямо востребовано?"
    maks "Ещё как! Хакеры каждый день находят лазейки, так что компании нуждаются в таких спецах, как Лера."
    anna "Ну, это наверное очень сложная работа, требующая много времени и усидчивости."

    menu:
        "Да это так, и я готов много и активно работать ":
            $ total_points += 1
            maks "Да это так, и я готов много и активно работать "
            anna "Ну, так иди за своей мечтой. Если тебя поддержат хотя бы друзья, то этого уже достаточно, правда?"

        "Не хочу сейчас об этом думать, мне кажется это не очень важно, как-нибудь справлюсь":
            $ total_points -= 1
            maks "Не хочу сейчас об этом думать, мне кажется это не очень важно, как-нибудь справлюсь"
            anna "Ну, как знаешь"

    if job_points == 1:
        maks "Спасибо, ребят. Без вас мне было бы тяжелее решиться на такой шаг"
    else:
        maks "Не знаю, мне нужно ещё обдумать всё наедине с собой."

    andreiy "Мы с тобой, Макс."
    
    scene black_screen with dissolve:
        zoom 1.41
    pause 1.5

    return
    

transform shake:
    xpos 0.5
    ypos 0.5
    linear 0.05 xpos 0.495 ypos 0.495
    linear 0.05 xpos 0.50 ypos 0.50
    repeat


label scene_six:
    scene bedroom with dissolve:
        zoom 1.41
    maks "Если я подпишу это, всё изменится. Не будет пути назад. Родители... Они никогда не поймут. Они всегда хотели лучшего для меня. А я что? Просто беру и перечёркиваю их надежды?"

    scene document with dissolve:
        zoom 1.41
    maks "А вдруг я ошибаюсь? А если это всего лишь глупый каприз? Может, я просто устал? Может, юрфак не так уж и плох, если привыкнуть. Но... я же чувствую, что это не моё. Каждый день словно пытка. Как можно привыкнуть к тому, что тебя убивает?"
    maks "Чёрт. Даже если что-то не получалось в программировании, я знал, что это то, что мне нравится. Я ведь всегда мечтал работать в айти. Но что, если я просто не подхожу для этого? Вдруг у меня не получится? Родители были правы —это слишком рискованно?"
    maks "Почему я должен выбирать? Почему нельзя просто... Просто жить, как хочется, без этого постоянного страха подвести кого- то?!"
    maks "Если я этого не сделаю... Я ведь буду жалеть. Всю жизнь. Но если сделаю, а всё пойдёт не так... Что тогда?"

    scene black with dissolve:
        zoom 1.41
    pause 1.0
    
    show text "Выбирай." at shake with dissolve
    pause 2.0
    hide text

    window show
    menu:
        "Отчислиться":
            if total_points == -2:
                jump alternative_ending
            else:
                jump good_ending
            
        "Остаться":
            jump bad_ending
            

label good_ending:
    pause 2.0

    scene colourful_library with dissolve:
        zoom 1.41

    show maks_end at Position(xpos = 0.2, ypos = 1.1)
    show lera at Position(xpos = 0.8, ypos = 1.1)


    lera "Привет, Макс! Как твои успехи? Втянулся?"
    maks "Привет! Уже получше, но иногда кажется, что информации столько, что взорвётся голова."
    lera "Ну, учиться на пентестера — это не просто, нужно освоить много технических навыков. Но ты вроде хорошо идёшь."
    maks "Пока так. Сейчас изучаю сканирование сетей, пробую разные инструменты. Это вообще-то захватывающе!"
    lera "Вот видишь? Это как в квесте: нашёл уязвимость, закрыл её — один шаг к успеху."
    maks "И эти инструменты… Nmap, Metasploit. Такое ощущение, что я — настоящий сыщик."
    lera "Уверена, ты быстро научишься находить слабости. Слушай, ты как вообще видишь свою работу?"
    maks "Я хочу быть профессионалом. Понимать, как всё работает, как «закрывать дыры». Помогать компаниям защищаться."
    lera "Вот это правильно. Важно понимать каждую деталь системы. Настоящий пентестер должен мыслить как хакер, чтобы предотвратить их действия"
    maks "Да, именно так! И при этом соблюдать закон, а не переходить грани."
    lera "Конечно, это и есть суть профессии. Мы помогаем защитить людей и их данные, это всегда востребовано."
    maks "И правда… Это не просто работа. Это как бы миссия."
    lera "Кстати, хочешь потренироваться на паре задачек? Я собрала тестовые уязвимости, попробуешь их обнаружить?"
    maks "Конечно! Я ждал этого момента."
    lera "Ладно, тогда готовься. Сначала проверим базовые вещи: порты, слабые пароли."
    maks "Хорошо, за дело!"

    scene black with dissolve
    "Спустя пару лет Макс заканчивает обучение и устраивается в ИТ- компанию. Его первый рабочий день. Мир вокруг теперь в ярких красках — словно заиграл новыми оттенками. Макс стоит перед зданием компании, ощущая гордость и радость"
    
    scene it_company with dissolve:
        zoom 1.41
    maks "Я это сделал. Наконец-то я на своём месте"
    "Пришло смс!"
    play sound "sms.mp3"

    "Начальник - здравствуйте Максим, не забудьте, мы сегодня ждём вас на работе."
    maks "Конечно, как никогда! Это первый день, которого я ждал"
    "{color=#00ff00}Хорошая концовка!{/color}"
    return

label alternative_ending:
    pause 2.0
    scene black with dissolve
    "«Макс сидит перед учебным корпусом нового университета. Всё началось с надежды, что этот шаг изменит его жизнь. "
    "Но спустя несколько месяцев всё стало иначе. Он чувствует, как радость от принятого решения угасает. Учёба сложнее, чем он ожидал. Мир, который он мечтал покорить, стал для него серым и холодным.»"
    scene library_it with dissolve:
        zoom 1.41
    show maks at Position(xpos = 0.5, ypos = 1.1)


    maks "Я думал, что всё изменится. Думал, что это мой шанс. А теперь, кажется, что я просто был глупцом. Тут всё не так, как я представлял... Код не пишется, задания застревают на полпути. Все вокруг знают больше, умеют лучше. Я же просто тону."
    
    show sokursnik at Position(xpos = 0.8, ypos = 1.1)
    show maks at Position(xpos = 0.2, ypos = 1.1)


    sokursnik "Эй, Макс, ты уже решил задачу по сетям? У меня с IP- адресами беда, может, глянешь?"
    maks "Нет, я даже не начинал."
    sokursnik "Как так? Мы ведь это ещё на прошлой неделе разобрали. Ты что, совсем забросил?"
    maks "Наверное, да…"

    "«Макс уходит домой»"

    scene bedroom with dissolve:
        zoom 1.41
    "Я так хотел... Так хотел быть здесь. Думал, что это будет моим спасением. Но почему я теперь чувствую себя ещё
хуже, чем на юрфаке? Там я хотя бы знал, что мне делать. "
    "А тут... Я ничего не понимаю. Все эти проекты, задачи, языки... Может, я
просто не создан для этого."
    "Может, я ошибся. Может, я вообще никуда не подхожу. Всё, что я делаю, просто рушится…"
    scene document with dissolve:
        zoom 1.41

    "{color=#FFFF00}Альтернативная концовка!{/color}"
    return

label bad_ending:
    pause 2.0
    "«Макс так и не смог поменять свою жизнь и остался юристом»"

    "{color=#FF0000}Плохая концовка!{/color}"
    return






